dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)

AC_INIT([MyRuLib],[0.23.1],[mail@lintest.ru],[myrulib],[http://myrulib.lintest.ru/])

AC_CONFIG_SRCDIR([MyRuLib/MyRuLibApp.cpp])
AC_CONFIG_SRCDIR([autoconf_inc.m4])

AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CXXCPP

dnl AC_PROG_INTLTOOL
dnl AM_GNU_GETTEXT

AM_OPTIONS_WXCONFIG
AM_PATH_WXCONFIG(2.8.10, WXFOUND=1)

if test "$WXFOUND" != 1; then
    AC_MSG_ERROR([
        Please check that wx-config is in path, the directory
        where wxWindows libraries are installed (returned by
        'wx-config --libs' command) is in LD_LIBRARY_PATH or
        equivalent variable and wxWindows is version 2.8.10 or above.
    ])
fi

AC_BAKEFILE([m4_include(autoconf_inc.m4)])

AC_MSG_CHECKING([for --with-expat])
AC_ARG_WITH([expat], [AS_HELP_STRING([--with-expat], [Use buildin Expat library])], USE_EXPAT="$withval", USE_EXPAT="no")
AC_MSG_RESULT([$USE_EXPAT])

if test $USE_EXPAT = no ; then 
	AC_CHECK_HEADER(expat.h,AC_CHECK_LIB(expat,XML_StopParser,USE_EXPAT="no",USE_EXPAT="yes"),USE_EXPAT="yes")
fi

if test $USE_EXPAT = yes ; then 
  AC_DEFINE(XML_STATIC)
  EXPAT_STATIC=1
  EXPAT_LIBRARY="expat_static"
  EXPAT_INCLUDE="Expat"
else
  EXPAT_STATIC=0
  EXPAT_SYSLIB=expat
fi

AC_MSG_CHECKING([for --with-sqlite])
AC_ARG_WITH([sqlite], [AS_HELP_STRING([--with-sqlite], [Use buildin SQLite3 library])], USE_SQLITE="$withval", USE_SQLITE="no")
AC_MSG_RESULT([$USE_SQLITE])

if test $USE_SQLITE = no ; then 
  AC_CHECK_HEADER(sqlite3.h, AC_CHECK_LIB(sqlite3, sqlite3_open,USE_SQLITE="no",USE_SQLITE="yes"),USE_SQLITE="yes")
fi

if test $USE_SQLITE = yes ; then 
  SQLITE_STATIC=1
  EXPAT_LIBRARY="sqlite3_static"
  SQLITE_INCLUDE="SQLite3"
else
  SQLITE_STATIC=0
  SQLITE_SYSLIB=sqlite3
fi
AC_SUBST(SQLITE_INCLUDE)

AC_MSG_CHECKING([for --with-locale])
AC_ARG_WITH([locale], [AS_HELP_STRING([--with-locale], [Include locale files into executable])], USE_LOCALE="$withval", USE_LOCALE="no")
AC_MSG_RESULT([$USE_LOCALE])

if test $USE_LOCALE = yes ; then 
  AC_DEFINE(FB_INCLUDE_LOCALE)
  INCLUDE_LOCALE=1
else
  INCLUDE_LOCALE=0
fi

AC_CONFIG_FILES([Makefile])

dnl report how we have been configured
echo
echo "Configured MyRuLib AC_PACKAGE_VERSION for \`${host}'"
echo ""
echo "  Use buildin Expat library?                ${USE_EXPAT}"
echo "  Use buildin SQLite3 library?              ${USE_SQLITE}"
echo "  Include locale files into executable?     ${USE_LOCALE}"
echo ""

AC_OUTPUT
